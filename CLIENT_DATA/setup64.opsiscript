[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	; Workarsound für non-silent Uninstaller (Version 0.16.6712). Austausch von 'uninstall.exe' vor Deinstallation 
	; Parameter: $SearchPattern$: Suchbegriff in Registry
	Set $SearchPattern$ = $ProductName$
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge
	if not ( count ($ResultList64$) = "0" )
		Set $RegId$ = takeString(0, $ResultList64$)
		Set $InstallDir$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] InstallLocation")
		Files_repair-uninstaller
	endif
	
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."
	comment "Starting the setup program"
	Winbatch_install
	sub_check_exitcode
	
	comment "Test for installation success"
	
	; Parameter: $SearchPattern$: Suchbegriff in Registry
	Set $SearchPattern$ = $ProductName$
	
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge
	
	if ( count ($ResultList64$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
		isFatalError
	else
		Set $RegId$ = takeString(0, $ResultList64$)
		comment "Get 'InstallLocation' from registry for path to exe"
		Set $PathToExe$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")
		; Workarsound für non-silent Uninstaller (Version 0.16.6712). Austausch von 'uninstall.exe'
		Set $InstallDir$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] InstallLocation")
		Files_repair-uninstaller		
		
		if $DesktopLink$ = "true"
			comment "Create common desktop link"
			Linkfolder_create_desktoplink
		endif
		
		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
			endif
		endif
	endif
	
endif

[Files_repair-uninstaller]
copy "%ScriptPath%\uninstall.exe" $InstallDir$

[Winbatch_install]
"%ScriptPath%\$InstFile$" /S

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
	name: $ProductName$
	target: $PathToExe$
end_link
