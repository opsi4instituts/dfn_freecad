requiredWinstVersion >= "4.12.0.13"

DefVar $InstFile$
DefVar $LogDir$
DefVar $ProductId$
DefVar $MinimumSpace$
DefVar $InstallDir$
DefVar $ExitCode$
DefVar $CustomPostInstall$
DefVar $CustomPostDeinstall$
DefVar $DesktopLink$
DefVar $ProductName$
DefVar $RegId$
DefVar $RegBitScope$
DefVar $PathToExe$
DefVar $UninstallProgram$
DefVar $INST_SystemType$
DefVar $MSVersion$
DefVar $ErrorString$
DefVar $ProductVersion$
DefVar $InstalledVersion$
DefStringlist $ResultList$

Set $LogDir$ = "%opsiLogDir%"

Set $INST_SystemType$ = GetSystemType
Set $MSVersion$	 = GetMsVersionInfo

Set $ProductVersion$	= takestring (0, splitString("%installingProdVersion%", "-"))
Set $DesktopLink$ = getProductProperty("desktop-link","false")

; ----------------------------------------------------------------
Set $ProductId$	        = "freecad"
Set $ProductName$       = "FreeCAD"
Set $MinimumSpace$	= "570 MB"
; ----------------------------------------------------------------

setloglevel = 6

; exitcode library von UIB (siehe C:\program files(x86)\opsi.org\opsi-client-agent\opsi-winst\lib) mit 'local functions'
importLib "uib_exitcode.opsiscript::isNsisExitcodeFatal"
; Library registry search uninstall keys (local functions)
importLib "func_search_registry_uninstall_keys.opsiinc"

if not ($MSVersion$ = "6.1" OR $MSVersion$ = "10.0")
	Logwarning "Only tested with windows 7 and 10! Please report to the maintainer successful installation on other windows versions."		
endif
if not ($INST_SystemType$ = "64 Bit System")
	LogError "At the moment a 32 bit version is not included in this packet!"
	isFatalError
endif

DefFunc getInstfileNameFromScriptPath(val $SearchTerm$ : string) : string
	DefStringList $list$
	Set $list$ = ShellCall('dir /b %ScriptPath%\' + $SearchTerm$)
	set $result$ = takeString(-1,$list$)
endfunc

Set $InstFile$ = getInstfileNameFromScriptPath($ProductId$ + "*.exe")
if not (contains($InstFile$, $ProductVersion$))
	LogError "Installfile does not contain version number."
	isFatalError 
endif

comment "Show product picture"
if (FileExists("%ScriptPath%\..\lib\icons\%installingProdName%.png"))
       comment "logo at local library/icons found."
       ShowBitmap "%ScriptPath%\..\lib\icons\%installingProdName%.png" $ProductName$
else
       comment "no logo at local library/icons, use from ScriptPath ..."
       ShowBitmap "%ScriptPath%\o4i.png" $ProductName$
endif